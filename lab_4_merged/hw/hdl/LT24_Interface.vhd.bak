--============================================================================
--! @file   LT24_Interface.vhd                                               -
--! @brief  Handles communication with ILI9341 driver                        -
--!         following 8080 I protocol.                                       -
--============================================================================

--! Use standard library
library IEEE;

--! Use logic elements
use IEEE.std_logic_1164.all;
use IEEE.numeric_std.all;

--============================================================================
--! Entities                                                                 -
--============================================================================

entity LT24_Interface is
   port (  
      --! clock, resets
      clk         :  in std_logic;
      nReset      :  in std_logic;
      
      --! 01 write command
      --! 10 write data
      --! 00 no operation
      cmd_or_data :  in std_logic_vector(1 downto 0);
      
      --! contains the command/data to write
      --! to start displaying data, cmd_data = '2C'
      cmd_data    :  in std_logic_vector(15 downto 0);
		
		--! Starts up or turns off the displayer
		lcd_on_in      : in  std_logic;
      
      --! LT24 signals
      LT24_CSX    :  out std_logic;
      LT24_DCX    :  out std_logic;
      LT24_D      :  out std_logic_vector(15 downto 0);
      LT24_RDX    :  out std_logic;
      LT24_WRX    :  out std_logic;
      LT24_Res_n  :  out std_logic;
		LT24_LCD_ON :  out std_logic;
      
      --! FIFO signals
      FIFO_empty  :  in std_logic;
      FIFO_q      :  in std_logic_vector(15 downto 0);
      FIFO_rdreq  :  out std_logic;
      
      --! '1' if writing cmd/data or displaying is completed
      cmd_data_ready : out std_logic;
   );
end entity LT24_Interface;


--============================================================================
--! Finite state machine                                                     -
--============================================================================

--! @brief     Architecture definition of the LT24 Interface.
--! @details   Handles communication with ILI9341 driver, following 8080-I 
--!            protocol. This module sends data from the FIFO or commands/data
--!            from the Avalon slave to the LT24. When the image is fully 
--!            displayed or the command has finished its execution, it sends
--!            back a confirmation pulse (cmd_data_ready).

architecture rtl of LT24_Interface is

   --! Constants -------------------------------------------------------------

   --! Command or data aliases
   constant TYPE_CMD    : std_logic_vector(1 downto 0) := "01";
   constant TYPE_DATA   : std_logic_vector(1 downto 0) := "10";
   constant TYPE_NO_OP  : std_logic_vector(1 downto 0) := "00";

   --! List of commands
   constant CMD_DISPLAY : std_logic_vector(7 downto 0) := x"2C";

   --! Image dimensions in pixels
   constant IMG_WIDTH      : positive := 320;
   constant IMG_HEIGHT     : positive := 240;

   --! Number of words per image
   constant IMG_WORD_LEN   : positive := IMG_WIDTH*IMG_HEIGHT;

   --! Variables -------------------------------------------------------------

   --! Used to generate proper delays following 8080 I protocol
   signal state_ready   : std_logic;
   signal cnt_cycles    : natural range 0 to 5;

   --! Keep track of the number of pixels displayed
   signal cnt           : natural range 0 to IMG_WORD_LEN;


   --! States ----------------------------------------------------------------
   type LT24_STATES is (
      INIT, 
      WRITE_CMD, WRITE_DATA, DISPLAY, 
      CHECK_FIFO, CHECK_TRANSFER
   );

   signal state, next_state : LT24_STATES;

   begin

      --! State transition logic ---------------------------------------------

      state_transition_logic : process(state, cmd_or_data, cnt, FIFO_empty, state_ready) is
      
         begin

            case state is
               when INIT            => if cmd_or_data = TYPE_CMD then
                                          next_state <= WRITE_CMD;
                                       elsif cmd_or_data = TYPE_DATA then
                                          next_state <= WRITE_DATA;
                                       end if;
               
               
               when WRITE_CMD       => if state_ready = '1' then
                                          if cmd_data(7 downto 0) = CMD_DISPLAY then
                                             next_state <= CHECK_FIFO;
                                          else
                                             next_state <= INIT;
                                          end if;
                                       end if;
                                       
               
               when WRITE_DATA      => if state_ready = '1' then
                                          next_state <= INIT;
                                       end if;
                                       
               
               when DISPLAY         => if state_ready = '1' then
                                          next_state <= CHECK_TRANSFER;
                                       end if;
                                       
               
               when CHECK_FIFO      => if FIFO_empty = '0' then
                                          next_state <= DISPLAY;
                                       end if;
               
               
               when CHECK_TRANSFER  => if cnt = IMG_WORD_LEN - 1 then
                                          next_state <= INIT;
                                       elsif FIFO_empty = '1' then
                                          next_state <= CHECK_FIFO;
                                       elsif FIFO_empty = '0' then
                                          next_state <= DISPLAY;
                                       end if;
               
                                          
               when others          => null;
               
            end case;
         
      end process state_transition_logic;
      
      
      --! State logic --------------------------------------------------------
      
      state_logic : process(nReset, clk) is
      
         begin
         
            if nReset = '0' then
            
               state          <= INIT;
               cnt            <= 0;
               cnt_cycles     <= 0;
               cmd_data_ready <= '0';
               state_ready    <= '0';
               
               LT24_CSX       <= '1';
               LT24_DCX       <= '1';
               LT24_RDX       <= '1';
               LT24_WRX       <= '1';
               LT24_D         <= (others => 'Z');
               LT24_Res_n     <= '0';
					LT24_LCD_on    <= '0';
               
            elsif rising_edge(clk) then
            
               state          <= next_state;
               state_ready    <= '0';
               
               
               case state is
               
                  when INIT            => cnt         <= 0;
                                          cnt_cycles  <= 0;
                                          
                                          LT24_Res_n  <= '1';
                                          LT24_CSX    <= '1';
                                          LT24_DCX    <= '1';
                                          LT24_RDX    <= '1';
                                          LT24_WRX    <= '1';
                                          LT24_D         <= (others => 'Z');
                                          
                                          cmd_data_ready <= '0';
                                          state_ready    <= '0';
                                                
                  
                  when WRITE_CMD       => -- ___         ___________  CSX
                                          --    \_______/
                                          -- ___         ___________  DCX
                                          --    \_______/
                                          -- ___     _______________  WRX
                                          --    \___/    
                                          -- ___ _______ ___________  D
                                          -- ___╳_______╳___________
                                          --             ___
                                          -- ___________/   \_______  cmd_data_ready/state_ready
                                          --
                                          --    0   1   2   3   4     cnt_cycles
                                          -- ---+---+---+---+---+---
                                          
                                          cnt_cycles <= cnt_cycles + 1;
                                          case cnt_cycles is
                                             when 0 =>
                                                LT24_CSX    <= '0';
                                                LT24_DCX    <= '0';  
                                                LT24_WRX    <= '0';
                                                LT24_D      <= cmd_data;
                                             when 1 =>
                                                LT24_WRX    <= '1';
                                             when 2 =>
                                                LT24_DCX    <= '1';
                                                LT24_CSX    <= '1';
                                                LT24_D      <= (others => 'Z');
                                                state_ready <= '1';
                                                cmd_data_ready <= '1';
                                             when others =>
                                                cnt_cycles  <= 0; 
                                                cmd_data_ready <= '0';
                                          end case;
                                          
                  
                  when WRITE_DATA      => -- ___         ___________  CSX
                                          --    \_______/
                                          -- _______________________  DCX
                                          --    
                                          -- ___     _______________  WRX
                                          --    \___/    
                                          -- ___ _______ ___________  D
                                          -- ___╳_______╳___________
                                          --             ___
                                          -- ___________/   \_______  cmd_data_ready/state_ready
                                          --
                                          --    0   1   2   3   4     cnt_cycles
                                          -- ---+---+---+---+---+---
                                          
                                          cnt_cycles <= cnt_cycles + 1;
                                          case cnt_cycles is
                                             when 0 =>
                                                LT24_CSX    <= '0';
                                                LT24_DCX    <= '1';  
                                                LT24_WRX    <= '0';
                                                LT24_D      <= cmd_data;
                                             when 1 =>
                                                LT24_WRX    <= '1';
                                             when 2 =>
                                                LT24_DCX    <= '1';
                                                LT24_CSX    <= '1';
                                                LT24_D      <= (others => 'Z');
                                                state_ready <= '1';
                                                cmd_data_ready <= '1';
                                             when others =>
                                                cmd_data_ready <= '0';
                                                cnt_cycles  <= 0; 
                                          end case;
                                          
                  
                  when DISPLAY         => -- _______         ___________  CSX
                                          --        \_______/
                                          -- ___________________________  DCX
                                          --    
                                          -- _______     _______________  WRX
                                          --        \___/    
                                          -- _______ _______ ___________  D
                                          -- _______╳_______╳___________
                                          --     ___
                                          -- ___/   \___________________  FIFO_rdreq
                                          --                 ___
                                          --  ______________/   \_______  state_ready
                                          --
                                          --    0   1   2   3   4   5     cnt_cycles
                                          -- ---+---+---+---+---+---+---
                  
                                          cnt_cycles <= cnt_cycles + 1;
                                          case cnt_cycles is
                                             when 0 =>
                                                FIFO_rdreq  <= '1';
                                             when 1 =>
                                                LT24_CSX    <= '0';
                                                LT24_DCX    <= '1';  
                                                LT24_WRX    <= '0';
                                                
                                                LT24_D      <= FIFO_q;
                                                FIFO_rdreq  <= '0';
                                             when 2 =>
                                                LT24_WRX    <= '1';
                                             when 3 =>
                                                LT24_DCX    <= '1';
                                                LT24_CSX    <= '1';
                                                LT24_D      <= (others => 'Z');
                                                state_ready <= '1';
                                             when others =>
                                                cnt_cycles  <= 0; 
                                          end case;
                                             
                                          
                  
                  when CHECK_FIFO      => null;
                  
                  
                  when CHECK_TRANSFER  => cnt <= cnt + 1;
                                          state_ready <= '0';
                                          --! Set cmd_data_ready at end of transfer
                                          if cnt = IMG_WORD_LEN - 1 then
                                             cmd_data_ready <= '1';
                                          end if;
                  
                                                                                    
                  when others          => null;
                  
               
               end case;
            end if;
      end process state_logic;
   

end architecture rtl;